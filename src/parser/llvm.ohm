LLVM {
  LLVM = Module

  // Lexical rules
  ident = (letter | digit | "$" | "." | "_")+
  stringLiteral = "\"" (~"\"" any)* "\""
  type = (letter | digit | "[" | "]" | "x" | "*" | "{" | "}" | "<" | ">" | "(" | ")" | "=" | "," | ".")+
  metadata = ","? space* "!" (~"\n" any)+
  comment = ";" (~"\n" any)*
  space += comment

  Module = TopLevel*
  
  TopLevel = 
    | Function
    | Declaration
    | GlobalAssignment
    | AttributeDef
    | MetadataDef
    | TargetDef
    | TypeAlias
    | SourceFilename
    | EmptyLine

  SourceFilename = "source_filename" "=" stringLiteral

  EmptyLine = space+

  // Function
  Function = "define" FuncHeader GlobalValue "(" Params? ")" FuncAttrs? "{" EntryBasicBlock BasicBlock* "}"
  Declaration = "declare" (~"\n" any)*
  GlobalAssignment = GlobalValue "=" (~"\n" any)*
  AttributeDef = "attributes" AttributeID "=" (~"\n" any)*
  MetadataDef = MetadataID "=" (~"\n" any)*
  TargetDef = "target" (~"\n" any)*
  TypeAlias = LocalValue "=" "type" (~"\n" any)*

  AttributeID = AttributeIDNumeric | AttributeIDString
  AttributeIDNumeric = "#" digit+
  AttributeIDString = "#" stringLiteral
  MetadataID = "!" (digit | ident | ".") +
  FuncHeader = (~GlobalValue any)*
  FuncAttrs = (~"{" any)*
  Params = Param ("," Param)*
  Param = (~(LocalValue &ParamTerminator) ~")" any)* LocalValue
  ParamTerminator = "," | ")"

  EntryBasicBlock = Label? BasicBlockItem* Terminator
  BasicBlock = Label BasicBlockItem* Terminator
  BasicBlockItem = DebugRecord | Instruction
  Label = ident ":"
  DebugRecord = "#" debugRecordContent
  debugRecordContent = (~"\n" any)*
  // Instruction
  Instruction = 
    | AssignInstruction
    | GenericInstruction
  AssignInstruction = LocalValue "=" opcode args metadata?
  GenericInstruction = opcode args metadata?
  opcode = ~"br" ~"ret" ident
  
  // args consumes everything until metadata or newline
  // Lexical rule (lowercase) ensures we don't skip newlines implicitly
  args = (~metadata ~"\n" any)*

  // metadata: optional comma, then !, then rest of line

  Terminator = 
    | BrInstruction
    | RetInstruction
    
  BrInstruction = "br" args metadata?
  RetInstruction = "ret" args metadata?

  GlobalValue = "@" (ident | stringLiteral)
  LocalValue = "%" (ident | stringLiteral)
  Value = GlobalValue | LocalValue | digit+
}
