SelectionDAG {
  Line = Node | Comment
  Node = space* NodeLHS space* "=" space* NodeRHS space*
  Comment = (~"\n" any)+
  NodeLHS = nodeId ":" Types
  NodeRHS
    = "ValueType" space* ":" space* type  -- valueType
    | opName Details Verbose? Operands? ExtraWrappedOperands? TailAttrs? Verbose?  -- normal

  Details = flag* Detail? Reg?
  flag
    = "nuw"
    | "nsw"
    | "exact"
    | "samesign"
    | "nneg"
    | "nnan"
    | "ninf"
    | "nsz"
    | "arcp"
    | "contract"
    | "afn"
    | "reassoc"
    | "nofpexcept"
  Detail = "<" DetailBody ">"
  DetailBody = (~">" any)*
  Verbose = "[" (~"]" any)+ "]"

  TailAttrs = TailAttr+
  TailAttr
    = Detail      -- detail
    | AttrAssign  -- assign
  AttrAssign = identChar+ "=" AttrValue
  AttrValue = (~space any)+

  Operands = Operand ("," Operand)*
  ExtraWrappedOperands = ExtraWrappedOperand+
  ExtraWrappedOperand = "<" NodeOperand ">"
  Operand
    = "<null>"              -- null
    | "<" NodeOperand ">"   -- wrappedNodeOp
    | NodeOperand            -- nodeOp
    | InlineOperand          -- inlineOp
    | Immediate              -- immediate
  NodeOperand = nodeId NodeIndex?
  NodeIndex = ":" digit+
  Immediate = "-"? digit+
  InlineOperand = opName ":" Types Details

  Types = Type ("," Type)*
  Type = type
  type = identChar+

  opName
    = unknownOpName    -- unknown
    | machineISDName   -- machine
    | identChar+       -- ident

  unknownOpName = "<<" (~">>" any)+ ">>"
  machineISDName = (~"::" identChar)+ "::" identChar+

  Reg
    = RegNoReg
    | RegStack
    | RegVirtReg
    | RegPhysReg
    | RegNumbered
    | RegBare
  RegNoReg = "$noreg"
  RegStack = "SS#" digit+
  RegVirtReg = "%" identChar+
  RegPhysReg = "$" identChar+
  RegNumbered = "#" digit+
  RegBare = upper identChar*

  nodeId
    = "t" digit+      -- tId
    | "0x" hexDigit+  -- hexId
  identChar = alnum | "_"
  space += "\t"
}
